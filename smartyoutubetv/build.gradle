buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        // These docs use an open ended version so that our plugin
        // can be updated quickly in response to Android tooling updates

        // We recommend changing it to the latest version from our changelog:
        // https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
// Put Fabric plugin after Android plugin
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion project.properties.projectCompileSdkVersion
    buildToolsVersion project.properties.projectBuildToolsVersion

    defaultConfig {
        minSdkVersion project.properties.projectMinSdkVersion
        targetSdkVersion project.properties.projectTargetSdkVersion
        applicationId "com.firsthash.smartyoutubetv2"
        versionCode 189
        versionName "6.12.29"
        multiDexEnabled = true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        orig {
        }
        V1080 {
            versionCode 999 // don't auto-update
        }
        Vkids {
            applicationIdSuffix ".kids"
        }
    }

    // naming example: SmartYouTubeTV_Xwalk_v6.8.12_r.apk
    // https://stackoverflow.com/questions/18332474/how-to-set-versionname-in-apk-filename-using-gradle
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def project = "SmartYouTubeTV"
            def flavor = variant.productFlavors[-1].name.capitalize()
            def buildType = variant.variantData.variantConfiguration.buildType.name.take(1)
            def version = variant.versionName

            def newApkName = sprintf("%s_%s_v%s_%s.apk", [project, flavor, version, buildType])

            output.outputFile = new File(output.outputFile.parent, newApkName)
        }
    }

    lintOptions {
        disable 'MissingTranslation'
        disable 'NewApi'
    }
}

dependencies {
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    testCompile 'commons-io:commons-io:2.5'
    testCompile 'org.robolectric:robolectric:3.5.1'

    compile fileTree(dir: 'libs', include: ['*.jar'])

    // uncomment if you want to use embeddable xwalk engine
    // originalFlavorCompile project(path: ':browser', configuration: "liteFlavorDebug")
    // betaFlavorCompile project(path: ':browser', configuration: "fullFlavorDebug")

    // Browser component
    debugCompile project(path: ':browser', configuration: "liteDebug")
    releaseCompile project(path: ':browser', configuration: "liteRelease")

    // Activity for ExoPlayer (taken for demo project)
    debugCompile project(path: ':exoplayeractivity', configuration: "debug")
    releaseCompile project(path: ':exoplayeractivity', configuration: "release")

    // AppUpdateChecker component
    debugCompile project(path: ':appupdatechecker', configuration: "debug")
    releaseCompile project(path: ':appupdatechecker', configuration: "release")

    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') { // see top part of this file
        transitive = true
    }
    compile 'cat.ereza:logcatreporter:1.2.0' // crashlytics logcat addon
    compile project.properties.projectAppCompatVersion
    compile 'com.noveogroup.android:android-logger:1.3.5'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta4'
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

    compile 'com.google.code.gson:gson:2.8.2'
}
